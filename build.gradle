buildscript {
    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
    dependencies {
        classpath 'uk.gov.ida:ida-gradle:1.1.0-15',
                  'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

plugins {
    id "org.sonarqube" version "2.2.1"
}

group = "uk.gov.ida"
version "1.0-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

apply plugin: 'idea'
apply plugin: "idaSonar"
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'java'
apply plugin: 'idaJar'

repositories {
    maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
}

dependencies {

    def dependencyVersions = [
        dropwizard:'1.0.5',
        ida_utils:'293'
    ]

    compile 'joda-time:joda-time:2.9.7',
            'com.google.guava:guava:21.0',
            'commons-lang:commons-lang:2.6',
            'commons-validator:commons-validator:1.5.1',
            'net.logstash.logback:logstash-logback-encoder:2.6',
            "io.dropwizard:dropwizard-core:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-configuration:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-auth:$dependencyVersions.dropwizard",
            "io.dropwizard:dropwizard-metrics-graphite:$dependencyVersions.dropwizard",
            'com.hubspot.dropwizard:dropwizard-guicier:1.0.0.6',
            "uk.gov.ida:rest-utils:2.0.0-$dependencyVersions.ida_utils",
            'org.mongodb:mongo-java-driver:2.13.3'

    testCompile 'junit:junit:4.11',
                'org.assertj:assertj-core:2.0.0',
                'org.assertj:assertj-joda-time:1.1.0',
                'org.assertj:assertj-guava:1.1.0',
                'org.mockito:mockito-core:1.10.17',
                "uk.gov.ida:common-utils:2.0.0-$dependencyVersions.ida_utils",
                "uk.gov.ida:rest-utils:2.0.0-$dependencyVersions.ida_utils",
                "uk.gov.ida:security-utils:2.0.0-$dependencyVersions.ida_utils",
                "io.dropwizard:dropwizard-client:$dependencyVersions.dropwizard",
                "io.dropwizard:dropwizard-testing:$dependencyVersions.dropwizard",
                "uk.gov.ida:common-test-utils:2.0.0-31"

}

import uk.gov.ida.RunConfig

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }

    workspace {

        iws {
            withXml { provider ->
                def runconfig = new RunConfig(provider)

                runconfig.ensureApplicationConfigurationExists(
                        'Rp Billing',
                        'uk.gov.ida.rpbilling.*',
                        'uk.gov.ida.rpbilling.RpBillingApplication',
                        'rp-billing',
                        'server configuration/rp-billing.yml')

                runconfig.ensureDebugConfigurationExists('Debug Rp Billing', '52192')
            }
        }

    }

    module {
        //if you love browsing Javadoc
        downloadJavadoc = true

        //and love reading sources :)
        downloadSources = true
    }

}

defaultTasks 'clean', 'zip'

